buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:0.12.1'
  }
}

apply plugin: 'android'

dependencies {
  compile project(':yota_sdk')
}

apply plugin:'base'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def NDK_BUILD = properties.getProperty('ndk.dir') + '/ndk-build'

task ndkBuild(type:Exec) {
  def clParts = ([NDK_BUILD, '-j', Runtime.runtime.availableProcessors() + 1] + propNdkFlags.split(' ')).flatten()
  commandLine clParts
}

task ndkBuildClean(type:Exec, description: 'Clean native libraries') {
  commandLine NDK_BUILD, 'clean'
}

task copyNativeLibs(type: Copy, dependsOn: 'ndkBuild', description: 'Copying native libraries.') {
  from(new File('libs')) { include '**/*.so' }
  into new File(buildDir, 'native-libs')
}

tasks.withType(Compile) { compileTask -> compileTask.dependsOn ndkBuild }
tasks.withType(Compile) { compileTask -> compileTask.dependsOn copyNativeLibs }

clean.dependsOn 'cleanCopyNativeLibs'
clean.dependsOn 'ndkBuildClean'

tasks.withType(com.android.build.gradle.tasks.PackageApplication) { pkgTask ->
  pkgTask.jniFolders = new HashSet<File>()
  pkgTask.jniFolders.add(new File(buildDir, 'native-libs'))
}

def getVersionName() { propVersionName }
def getApkName()     { propApkName }
def getBaseName()    { getApkName() + "_" + getVersionName() }


android {
  // All properties are read from gradle.properties file
  // Should we use "Yota Platinum:Platinum Add-On:17" as compile SDK version?
  compileSdkVersion propTargetSdkVersion.toInteger()
  buildToolsVersion propBuildToolsVersion

  defaultConfig {
    versionCode propVersionCode.toInteger()
    versionName propVersionName
    minSdkVersion propMinSdkVersion.toInteger()
    targetSdkVersion propTargetSdkVersion.toInteger()
  }

  project.archivesBaseName = getBaseName()

  signingConfigs {
    yopme {
      storeFile file("key/android.key")
      storePassword "***REMOVED***"
      keyAlias "yopme"
      keyPassword "***REMOVED***"
    }
  }

  buildTypes {
    debug {
      jniDebugBuild true
    }

    release {
      signingConfig signingConfigs.yopme
    }
  }

  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs = ['src']
      res.srcDirs = ['res']
      assets.srcDirs = ['assets']
    }
  }

  // We don't compress these extensions in assets/ because our random FileReader can't read zip-compressed files from apk
  aaptOptions {
    noCompress 'txt', 'bin', 'skn', 'html', 'png', 'json', 'mwm', 'ttf'
    ignoreAssetsPattern "!.svn:!.git:!.DS_Store:!*.scc:.*:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*~"
  }

  lintOptions {
    checkReleaseBuilds false
    abortOnError false
  }
}
