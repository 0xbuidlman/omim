buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath propGradleAndroidPlugin
  }
}

apply plugin: 'android'
apply from: 'secure.properties'

dependencies {
  compile project(':yota_sdk')
}

apply plugin:'base'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def NDK_BUILD = properties.getProperty('ndk.dir') + '/ndk-build'

task ndkBuild(type:Exec) {
  def clParts = ([NDK_BUILD, '-j', Runtime.runtime.availableProcessors() + 1] + propNdkFlags.split(' ')).flatten()
  commandLine clParts
}

task ndkBuildClean(type:Exec, description: 'Clean native libraries') {
  commandLine NDK_BUILD, 'clean'
}

tasks.withType(JavaCompile) { compileTask -> compileTask.dependsOn ndkBuild }

clean.dependsOn 'ndkBuildClean'

def getVersionName() { propVersionName }
def getApkName()     { propApkName }
def getBaseName()    { getApkName() + "_" + getVersionName() }


android {
  // All properties are read from gradle.properties file
  // Should we use "Yota Platinum:Platinum Add-On:17" as compile SDK version?
  compileSdkVersion propTargetSdkVersion.toInteger()
  buildToolsVersion propBuildToolsVersion

  defaultConfig {
    versionCode propVersionCode.toInteger()
    versionName propVersionName
    minSdkVersion propMinSdkVersion.toInteger()
    targetSdkVersion propTargetSdkVersion.toInteger()
  }

  project.archivesBaseName = getBaseName()

  signingConfigs {
    yopme {
      storeFile file(spropYotaStoreFile)
      storePassword spropYotaStorePassword
      keyAlias spropYotaKeyAlias
      keyPassword spropYotaKeyPassword
    }
  }

  buildTypes {
    debug {
      jniDebugBuild true
    }

    release {
      signingConfig signingConfigs.yopme
    }
  }

  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs = ['src']
      res.srcDirs = ['res']
      assets.srcDirs = ['assets']
      jniLibs.srcDirs = ['libs']
    }
  }

  // We don't compress these extensions in assets/ because our random FileReader can't read zip-compressed files from apk
  aaptOptions {
    noCompress 'txt', 'bin', 'skn', 'html', 'png', 'json', 'mwm', 'ttf'
    ignoreAssetsPattern "!.svn:!.git:!.DS_Store:!*.scc:.*:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*~"
  }

  lintOptions {
    checkReleaseBuilds false
    abortOnError false
  }
}
